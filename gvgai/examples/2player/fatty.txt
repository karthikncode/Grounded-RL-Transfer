BasicGame no_players=2 key_handler=Pulse
    SpriteSet
        ground > Immovable hidden=True img=oryx/backGrey

        door > Door
            doorOpen > img=oryx/dooropen1
            doorClosed > img=oryx/doorclosed1

        ingredient > Resource img=newset/flour shrinkfactor=0.5
            eat > value=1 limit=4
            bake > value=1 limit=10

        structures > Immovable
            oven > color=GRAY img=newset/city1_fire
            bed > color=BLUE img=newset/street2
            blank > hidden=True invisible=True
            cake > color=PINK img=newset/logm

        avatar > MovingAvatar frameRate=16
            child > img=newset/man4 shrinkfactor=0.7
            mum > img=newset/girl3

        wall > Immovable img=oryx/wall3 autotiling=True
        
    InteractionSet
        avatar avatar wall > stepBack
        mum bed > stepBack
        child oven doorClosed > stepBack

        eat child > collectResource killResource=True scoreChange=2,0
        bake child > killSprite
        child eat > killIfHasMore resource=eat limit=4
        child bed > spawnIfHasMore limit=3 resource=eat spend=3 stype=blank
        child bed > transformToAll stype=doorOpen stypeTo=doorClosed
        child bed > addTimer timer=100 ftype=transformToAll stype=doorClosed stypeTo=doorOpen
        blank bed > killSprite

        eat mum > killSprite
        bake mum > collectResource killResource=True

        mum oven > spawnIfHasMore limit=5 resource=bake spend=5 stype=cake scoreChange=0,20

    TerminationSet
        MultiSpriteCounter stype1=cake limit=1 win=False,True
        SpriteCounter stype=child win=False,True
        SpriteCounter stype=ingredient win=True,False
        Timeout limit=1500 win=True,False
    
    LevelMapping
        A > child ground
        B > mum ground
        x > eat bake ground
        b > bed ground
        o > oven ground
        . > ground
        d > doorOpen ground

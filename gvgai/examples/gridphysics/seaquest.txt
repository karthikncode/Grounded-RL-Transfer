BasicGame
    SpriteSet

        sky   > Immovable img=oryx/backLBlue
        water > Immovable img=newset/water2
        saved > Immovable color=LIGHTGREEN
        holes > SpawnPoint color=LIGHTGRAY img=newset/whirlpool2 portal=True
            sharkhole  >  stype=shark  prob=0.01
            whalehole  >  stype=whale  prob=0.005
            diverhole  >  stype=diver
                normaldiverhole > prob=0.005
                oftendiverhole  > prob=0.025

        moving >
            avatar  > ShootAvatar color=YELLOW  stype=torpedo img=newset/submarine healthPoints=18 limitHealthPoints=20
            torpedo > Missile color=YELLOW img=oryx/bullet1
            fish >
                shark  > Missile orientation=LEFT  speed=0.25 color=ORANGE img=newset/shark2
                whale  > Bomber  orientation=RIGHT  speed=0.1 color=BROWN stype=pirana prob=0.02 img=newset/whale
                pirana > Missile orientation=RIGHT speed=0.25 color=RED shrinkfactor=0.6 img=newset/piranha2
            diver > RandomNPC color=GREEN speed=0.5 img=newset/diver1 cons=2

        crew  > Resource color=GREEN limit=4

    InteractionSet

        avatar TIME > subtractHealthPoints timer=26 repeating=True

        EOS avatar diver sky   > stepBack
        fish EOS  > killSprite #Note that from vgdl 2.0, EOS can be the first or the second sprite.

        fish torpedo > killBoth scoreChange=1
        avatar fish  > killSprite

        avatar sky > addHealthPoints value=1
        avatar sky > spawnIfHasMore resource=crew stype=saved limit=4 spend=4
        saved  sky > killSprite scoreChange=1000

        avatar diver > changeResource resource=crew
        diver avatar > killSprite

    LevelMapping
        a > sky
        A > water avatar
        1 > water sharkhole
        2 > water whalehole
        3 > water normaldiverhole
        4 > water oftendiverhole
        . > water

    TerminationSet
        SpriteCounter      stype=avatar               limit=0 win=False
        Timeout limit=1000 win=True
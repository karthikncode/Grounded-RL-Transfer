BasicGame square_size=30
    SpriteSet

        floor > Immovable img=oryx/backBiege hidden=True

        base    > Immovable    color=WHITE img=oryx/wall6
        avatar  > ShootAvatar stype=bullet healthPoints=5 img=newset/cop1
        bullet > Missile img=oryx/orb3 shrinkfactor=0.5 singleton=True

        turn > Immovable invisible=True hidden=True
            turnleft > orientation=LEFT img=dirt
            turnright > orientation=RIGHT img=dirt

        bandit > Bomber prob=0.1 cooldown=6 speed=1
            bleftd > orientation=LEFT spawnorientation=DOWN stype=missile img=newset/bandit1h
            brightu > orientation=RIGHT spawnorientation=UP stype=missile img=newset/bandit1
            bleftu > orientation=LEFT spawnorientation=UP stype=missile img=newset/bandit1h
            brightd > orientation=RIGHT spawnorientation=DOWN stype=missile img=newset/bandit1

        missile > Missile img=oryx/orb1 shrinkfactor=0.5 speed=0.6
        exit > Door
            emptyExit > img=oryx/door2
            witExit > img=oryx/door2

        witness > img=newset/girl2
            running > Chaser stype=exit cooldown=5 color=RED speed=0.4
            staying > Immovable

        wall > Immovable img=oryx/dirtwall autotiling=True

    LevelMapping
        0 > floor base
        l > floor bleftd
        r > floor brightd
        . > floor bleftu
        , > floor brightu
        A > floor avatar
        1 > floor turnup
        2 > floor turnleft
        3 > floor turndown
        4 > floor turnright
        n > floor running
        e > floor emptyExit
        _ > floor

    TerminationSet
        SpriteCounter      stype=avatar           limit=0 win=False
        SpriteCounter      stype=witness          limit=0 win=False
        SpriteCounter      stype=exit             limit=0 win=True
        
    InteractionSet

        avatar  base  > stepBack
        avatar  wall > stepBack

        base missile > killBoth
        base bullet > killBoth
        missile bullet > killBoth
        bullet wall > killSprite

        missile EOS > killSprite
        bullet EOS > killSprite

        bleftd turnright > transformTo stype=brightd
        bleftu turnright > transformTo stype=brightu
        brightd turnleft > transformTo stype=bleftd
        brightu turnleft > transformTo stype=bleftu

        bandit bullet > killBoth scoreChange=1


        avatar missile > subtractHealthPoints

        missile avatar > killSprite
        missile witness > killBoth

        emptyExit witness > transformTo stype=witExit scoreChange=5
        witness witExit > transformTo stype=staying
        witExit avatar > killSprite

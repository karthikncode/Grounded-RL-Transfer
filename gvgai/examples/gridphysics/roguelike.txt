BasicGame square_size=25
    SpriteSet
        floor > Immovable hidden=True img=oryx/floor3
        exit > Door color=GREEN img=newset/exit2

        sword > Flicker limit=5 singleton=True img=oryx/sword4 shrinkfactor=0.8
        weapon > Resource color=BLUE value=10000 limit=10000 img=oryx/sword4 shrinkfactor=0.8
        health > Resource img=oryx/heart1 shrinkfactor=0.8 limit=10


        lock > Immovable img=newset/lock1
        gold > Resource color=GOLD limit=11 img=oryx/gold2 shrinkfactor=0.5
        key > Resource img=oryx/key2 limit=1 shrinkfactor=0.7

        market > Immovable img=newset/city1_1
        transaction > Immovable invisible=True

        moving >
            avatar > ShootAvatar ammo=weapon stype=sword img=oryx/swordman1 frameRate=8 healthPoints=1 limitHealthPoints=10
            spider > RandomNPC speed=1 cooldown=4 img=oryx/spider2 cons=3
            phantom > Chaser stype=avatar cooldown=6 speed=0.5 color=BROWN  img=oryx/ghost2

        wall > Immovable img=oryx/wall3 autotiling=True

    InteractionSet
        weapon avatar   > collectResource scoreChange=2
        gold avatar     > collectResource scoreChange=1
        key avatar      > collectResource scoreChange=1

        lock avatar > killIfOtherHasMore resource=key limit=1
        moving wall lock > stepBack pixelPerfect=True

        exit avatar > killSprite scoreChange=10

        avatar spider > subtractHealthPoints value=2
        spider sword > killSprite scoreChange=2

        avatar phantom > subtractHealthPoints value=1
        phantom sword > killSprite scoreChange=1

        avatar market > spawnIfHasMore resource=gold limit=1 stype=health spend=1
        avatar health  > addHealthPoints value=1 killSecond=True

    TerminationSet
        SpriteCounter stype=avatar limit=0 win=False
        SpriteCounter stype=exit limit=0 win=True

    LevelMapping
        x > exit floor
        s > weapon floor
        g > gold floor
        r > spider floor
        p > phantom floor
        h > health floor
        k > key floor
        l > lock floor
        m > market floor
        A > avatar floor
        . > floor